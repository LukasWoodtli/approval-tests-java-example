plugins {
    id 'java'
}

group 'org.lukas.woodtli.approval-tests-java-example'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}


// Integration tests
sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
        }

        resources.srcDir file('src/integration-test/resources')
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
    dependsOn build
}


dependencies {
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.30'
    compile group: 'org.slf4j', name: 'log4j-over-slf4j', version: '1.7.30'
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.13.2'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.13.2'
    compile group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.13.2'
    compile group: 'org.xerial', name:'sqlite-jdbc', version:'3.30.1'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    integrationTestCompile group: 'com.approvaltests', name: 'approvaltests', version: '4.0.2'
    integrationTestCompile group: 'junit', name: 'junit', version: '4.12'
    integrationTestCompile group: 'com.google.code.gson', name: 'gson', version: '2.8.6'
    integrationTestCompile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.13.2'
    integrationTestCompile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.13.2', classifier: 'tests'

    integrationTestCompile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.5'
    integrationTestCompile group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: '2.9.5'
}
